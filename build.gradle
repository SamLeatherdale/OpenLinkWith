buildscript {
    ext.ci = 'true' == System.getenv('CI')
    ext.configFile = { fileName ->
        rootProject.file("gradle/$fileName")
    }

    ext.playPublishTrack = 'internal'

    ext.versions = [
            compileSdk : 35, // Android 15
            minSdk     : 23,
            versionCode: 30300,
            versionName: "3.3.0",

            dagger     : '2.56.2',
            lifecycle  : '2.4.0',
            okHttp     : '4.8.0',
            room       : '2.4.0',
    ]

    dependencies {
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.40.0'
        classpath 'io.gitlab.arturbosch.detekt:detekt-gradle-plugin:1.14.1'
        classpath 'com.gradleup:static-analysis-plugin:1.4'
    }
}

plugins {
    id 'com.android.application' version '8.11.1' apply false
    id 'com.android.library' version '8.11.1' apply false
    id 'org.jetbrains.kotlin.android' version '2.1.21' apply false
    id 'org.jetbrains.kotlin.plugin.parcelize' version '2.1.21' apply false
    id 'com.google.gms.google-services' version '4.4.2' apply false
    id 'com.google.firebase.crashlytics' version '3.0.3' apply false
}

allprojects {
    if (project.name != 'test-support' && project.name != 'base-translations') {
        apply from: configFile('static-analysis.gradle')
    }
}

apply from: configFile('configure-dependency-updates.gradle')
